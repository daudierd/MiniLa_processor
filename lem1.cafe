-- ----------------------------------------------------------
--                                                         --
--       MiniLa language processor verified compiler       --
--                 PROOF  SCORES - LEMMA 1                 --
--                                                         --
--                     DAUDIER, Dorian                     --
--            Special research student at JAIST            --
--                                                         --
-- ----------------------------------------------------------

in minila.cafe
in del.cafe
in eval.cafe

-- -------------------------------------------
-- LEMMA Y PROOFS CAN BE FOUND IN lemY.cafe --
-- -------------------------------------------

-- len(IL @ (I | iln)) = s(len(IL))
-- len(IL @ genExp(E) @ (I | iln)) = s(len(IL @ genExp(E)))
-- len(IL @ genExp(E1) @ genExp(E2) @ (I | iln)) = s(len(IL @ genExp(E1) @ genExp(E2)))

	-- nb: lemma X is no longer needed
	
-- ---------------------------------------------
-- NTH-DEL PROOF CAN BE FOUND IN nth-del.cafe --
-- ---------------------------------------------

-- nth(N, IL) = hd(del(N, IL))


-- -----------
-- LEMMA 1E --
-- -----------

-- exec(IL1 @ genExp(E) @ IL2, len(IL1), SE, EE) = exec(IL1 @ genExp(E) @ IL2, len(genExp(E)) + len(IL1), evalExp(E, EE) | SE, EE)

-- BASE CASE
	-- Exp -> PNat
open VERIFY-COMP .
	pr(DEL)
	op en : -> ExpPNat .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	op ev : -> Env .
	
	-- lemma Pev
	eq evalExp(EN, EV) = en2n(EN) .
	-- Nth-del theorem
	eq nth(N, IL) = hd(del(N, IL)) .
	-- check
	red lem1E(en, il1, il2, stk, errEnv) .
	red lem1E(en, il1, il2, stk, ev) .
close

	-- Exp -> Var
open VERIFY-COMP .
	pr(DEL)
	op v : -> Var .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	op ev : -> Env .
	
	-- lemma Pev
	eq evalExp(EN, EV) = en2n(EN) .
	-- Nth-del theorem
	eq nth(N, IL) = hd(del(N, IL)) .
	-- check
	red lem1E(v, il1, il2, stk, errEnv) .
	red lem1E(v, il1, il2, stk, ev) .
close

-- INDUCTION CASE
open VERIFY-COMP .
	pr(DEL)
	ops e e1 e2 : -> Exp .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	op ev : -> Env .
	
	-- IH
	eq exec(IL1 @ genExp(e1) @ IL2, len(IL1), SE, EE) = exec(IL1 @ genExp(e1) @ IL2, len(genExp(e1)) + len(IL1), evalExp(e1, EE) | SE, EE) .
	eq exec(IL1 @ genExp(e2) @ IL2, len(IL1), SE, EE) = exec(IL1 @ genExp(e2) @ IL2, len(genExp(e2)) + len(IL1), evalExp(e2, EE) | SE, EE) .
	
	-- lemma Ladd
	eq len(IL1) + len(IL2) = len(IL1 @ IL2) .
	-- Nth-del theorem
	eq nth(N, IL) = hd(del(N, IL)) .
	-- lemma Y-2
	eq len(IL @ genExp(E1) @ genExp(E2) @ (I | iln)) = s(len(IL @ genExp(E1) @ genExp(E2))) .
	
	-- check
	red lem1E(e, il1, il2, stk, errEnv) .
	red lem1E(e1 + e2, il1, il2, stk, ev) .
	red lem1E(sd(e1,e2), il1, il2, stk, ev) .
	red lem1E(e1 * e2, il1, il2, stk, ev) .
	red lem1E(e1 / e2, il1, il2, stk, ev) .
	red lem1E(e1 % e2, il1, il2, stk, ev) .
	
	-- Boolean operations
		-- e1 and e2 evaluate as natural numbers
		ops n1 n2 : -> PNat .
		eq evalExp(e1,ev) = n1 .
		eq evalExp(e2,ev) = n2 .
	red lem1E(e1 < e2, il1, il2, stk, ev) .
	red lem1E(e1 > e2, il1, il2, stk, ev) .
	red lem1E(e1 === e2, il1, il2, stk, ev) .
	red lem1E(e1 =!= e2, il1, il2, stk, ev) .
	red lem1E(e1 && e2, il1, il2, stk, ev) .
	red lem1E(e1 || e2, il1, il2, stk, ev) .
close

-- -----------
-- LEMMA 1S --
-- -----------

-- exec(gen(S) @ IL, SE, EE) = exec(IL, SE, eval(S, EE))

-- BASE CASE
	-- S = estm
open VERIFY-COMP .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	op ev : -> Env .
	-- check
	red lem1S(estm, il1, il2, stk, errEnv) .
	red lem1S(estm, il1, il2, stk, ev) .
close

	-- S = (V := E ;)
open VERIFY-COMP .
	pr(DEL)
	op v : -> Var .
	op e : -> Exp .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	op ev : -> Env .
	
	-- lemma Ladd
	eq len(IL1) + len(IL2) = len(IL1 @ IL2) .
	-- Nth-del theorem
	eq nth(N, IL) = hd(del(N, IL)) .
	-- lemma Y-1
	eq len(IL @ genExp(E) @ (I | iln)) = s(len(IL @ genExp(E))) .
	-- lemma 1E
	eq exec(IL1 @ genExp(E) @ IL2, len(IL1), SE, EE) = exec(IL1 @ genExp(E) @ IL2, len(genExp(E)) + len(IL1), evalExp(E, EE) | SE, EE) .
	
	-- check
	red lem1S(v := e ;, il1, il2, stk, errEnv) .
	red lem1S(v := e ;, il1, il2, stk, ev) .
close

-- PRELIMINARY: Lemma 1S with exec2 & hd
-- exec2(hd(gen(S) @ IL2), IL1 @ gen(S) @ IL2, len(IL1), SE, EV = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EV))
open VERIFY-COMP .
	pr(DEL)
	op s : -> Stm .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	op ev : -> Env .
	
	-- Nth-del theorem
		eq nth(N, IL) = hd(del(N, IL)) .
		
	-- CHECK
		-- PART 1
		red exec2(nth(0, gen(s) @ il2), il1 @ gen(s) @ il2, len(il1), stk, ev) = exec(il1 @ gen(s) @ il2, len(il1), stk, ev) .
		
		-- lemma 1S
		eq exec(IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .

		-- PART 2
		red exec2(hd(gen(s) @ il2), il1 @ gen(s) @ il2, len(il1), stk, ev) = exec2(nth(0, gen(s) @ il2), il1 @ gen(s) @ il2, len(il1), stk, ev) .
		red exec(il1 @ gen(s) @ il2, len(il1), stk, ev) = exec(il1 @ gen(s) @ il2, len(il1 @ gen(s)), stk, eval(s, ev)) .
close

-- INDUCTION BY CONDITIONAL STRUCTURE
	-- CASE 1 : Case splitting, evalExp(e, ev) = 0
	open VERIFY-COMP .
		pr(DEL)
		op e : -> Exp .
		ops s1 s2 : -> Stm .
		ops il1 il2 : -> IList .
		op stk : -> Stack&Err .
		op ev : -> Env .
		
	-- HYPOTHESIS
		-- Case splitting : evalExp(e, ev) = 0
		eq evalExp(e,ev) = 0 .
		-- lemma 1S (Induction Hypothesis)
		eq exec(IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .
		-- lemma 1S (Induction Hypothesis, with exec2 & hd)
		eq exec2(hd(gen(S) @ IL2), IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .
		
	-- LEMMAS
		-- lemma Ladd (reversed)
		eq len(IL2 @ IL1) = len(IL1) + len(IL2) .
		-- lemma 1E
		eq exec(IL1 @ genExp(E) @ IL2, len(IL1), SE, EE) = exec(IL1 @ genExp(E) @ IL2, len(IL1) + len(genExp(E)), evalExp(E, EE) | SE, EE) .
		-- Nth-del theorem
		eq nth(N, IL) = hd(del(N, IL)) .
		-- lemma Y-1
		eq len(IL @ genExp(E) @ (I | iln)) = s(len(IL @ genExp(E))) .
		
		-- CHECK: 3-stepped verification
			-- We use a 3-stepped verification combined with strategy chosen for exec2, to avoid parenthesis reorganization and use lemma 1S
			op pca : -> PNat .
			ops ila ilb : -> IList .
			
			eq pca = s(s(s((len(il1) + (len(gen(s1)) + len(genExp(e))))))) .
			eq ila = (il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s1))))) | (gen(s1) @ (jump(s(len(gen(s2)))) | (gen(s2) @ il2))))))) .
			
			eq ilb = il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s1))))) | (gen(s1) @ (jump(s(len(gen(s2)))) | iln))))) .
			
		-- step 1 : LHS = f(X)
			red exec(il1 @ gen(if (e) { s1 } else { s2 }) @ il2, len(il1), stk, ev) = exec2(hd(gen(s2) @ il2),ila,pca,stk,ev) .
			
		-- step 2 : X = Y
			red ila = ilb @ gen(s2) @ il2 .
			red len(ilb) = pca .
			
		-- step 3 : f(Y) = RHS
			red exec2(hd(gen(s2) @ il2), ilb @ gen(s2) @ il2, len(ilb), stk, ev) = exec(il1 @ gen(if (e) { s1 } else { s2 }) @ il2, len(il1 @ gen(if (e) { s1 } else { s2 })), stk, eval(if (e) { s1 } else { s2 }, ev)) .
	close
	
	-- CASE 2.1 : Case splitting, evalExp(e, ev) > 0
	-- eval(s1,empEnv) has no error
	open VERIFY-COMP .
		pr(DEL)
		op e : -> Exp .
		ops s1 s2 : -> Stm .
		ops il1 il2 : -> IList .
		op stk : -> Stack&Err .
		ops ev ev2 : -> Env .
	-- HYPOTHESIS
		-- Case splitting : evalExp(e, ev) > 0
		eq evalExp(e,ev) = s(N) .
		-- eval(s1,ev) is not errEnv
		eq eval(s1,ev) = ev2 .
		-- lemma 1S (Induction Hypothesis)
		eq exec(IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .
		-- lemma 1S (Induction Hypothesis, with exec2 & hd)
		eq exec2(hd(gen(S) @ IL2), IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .
		
	-- LEMMAS
		-- lemma Ladd (reversed)
		eq len(IL2 @ IL1) = len(IL1) + len(IL2) .
		-- lemma 1E
		eq exec(IL1 @ genExp(E) @ IL2, len(IL1), SE, EE) = exec(IL1 @ genExp(E) @ IL2, len(IL1) + len(genExp(E)), evalExp(E, EE) | SE, EE) .
		-- Nth-del theorem
		eq nth(N, IL) = hd(del(N, IL)) .
		-- lemma Y-1
		eq len(IL @ genExp(E) @ (I | iln)) = s(len(IL @ genExp(E))) .

	-- CHECK: 3-stepped verification
			op pca : -> PNat .
			ops ila il1b il2b : -> IList .
			
			eq pca = s(s((len(genExp(e)) + len(il1)))) .
			eq ila = (il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s1))))) | (gen(s1) @ (jump(s(len(gen(s2)))) | (gen(s2) @ il2))))))) .
			
			eq il1b = il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s1))))) | iln))) .
			eq il2b = (jump(s(len(gen(s2)))) | (gen(s2) @ il2)) .
		
	-- step 1 : LHS = f(X)
		red exec(il1 @ gen(if (e) { s1 } else { s2 }) @ il2, len(il1), stk, ev) = exec2(hd(gen(s1) @ il2b),ila,pca,stk,ev) .
		
	-- step 2 : X = Y
		red ila = il1b @ gen(s1) @ il2b .
		red len(il1b) = pca .
		
	-- step 3 : f(Y) = RHS
		red exec2(hd(gen(s1) @ il2b), il1b @ gen(s1) @ il2b, len(il1b), stk, ev) = exec(il1 @ gen(if (e) { s1 } else { s2 }) @ il2, len(il1 @ gen(if (e) { s1 } else { s2 })), stk, eval(if (e) { s1 } else { s2 }, ev)) .
	close
	
	-- CASE 2.2 : Case splitting, evalExp(e, ev) > 0
	-- eval(s1,ev) has error
	open VERIFY-COMP .
	pr(DEL)
	op e : -> Exp .
	ops s1 s2 : -> Stm .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	ops ev : -> Env .

	-- HYPOTHESIS
		-- Case splitting : evalExp(e, ev) > 0
		eq evalExp(e,ev) = s(N) .
		-- eval(s1,ev) is errEnv
		eq eval(s1,ev) = errEnv .
		-- lemma 1S (Induction Hypothesis)
		eq exec(IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .
		-- lemma 1S (Induction Hypothesis, with exec2 & hd)
		eq exec2(hd(gen(S) @ IL2), IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec(IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .

	-- LEMMAS
		-- lemma Ladd (reversed)
		eq len(IL2 @ IL1) = len(IL1) + len(IL2) .
		-- lemma 1E
		eq exec(IL1 @ genExp(E) @ IL2, len(IL1), SE, EE) = exec(IL1 @ genExp(E) @ IL2, len(IL1) + len(genExp(E)), evalExp(E, EE) | SE, EE) .
		-- Nth-del theorem
		eq nth(N, IL) = hd(del(N, IL)) .
		-- lemma Y-1
		eq len(IL @ genExp(E) @ (I | iln)) = s(len(IL @ genExp(E))) .

	-- CHECK: 3-stepped verification
			op pca : -> PNat .
			ops ila il1b il2b : -> IList .
			
			eq pca = s(s((len(genExp(e)) + len(il1)))) .
			eq ila = (il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s1))))) | (gen(s1) @ (jump(s(len(gen(s2)))) | (gen(s2) @ il2))))))) .
			
			eq il1b = il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s1))))) | iln))) .
			eq il2b = (jump(s(len(gen(s2)))) | (gen(s2) @ il2)) .
		
	-- step 1 : LHS = f(X)
		red exec(il1 @ gen(if (e) { s1 } else { s2 }) @ il2, len(il1), stk, ev) = exec2(hd(gen(s1) @ il2b),ila,pca,stk,ev) .
		
	-- step 2 : X = Y
		red ila = il1b @ gen(s1) @ il2b .
		red len(il1b) = pca .
		
	-- step 3 : f(Y) = RHS
		red exec2(hd(gen(s1) @ il2b), il1b @ gen(s1) @ il2b, len(il1b), stk, ev) = exec(il1 @ gen(if (e) { s1 } else { s2 }) @ il2, len(il1 @ gen(if (e) { s1 } else { s2 })), stk, eval(if (e) { s1 } else { s2 }, ev)) .
	close


-- INDUCTION BY SEQUENTIAL COMPOSITION
open VERIFY-COMP .
	ops s1 s2 : -> Stm .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	op ev : -> Env .
	-- lemma Ladd
	eq len(IL1) + len(IL2) = len(IL1 @ IL2) .
	-- IH
	eq exec(IL @ gen(s1) @ IL2, len(IL), SE, EE) = exec(IL @ gen(s1) @ IL2, len(gen(s1)) + len(IL), SE, eval(s1, EE)) .
	eq exec(IL @ gen(s2) @ IL2, len(IL), SE, EE) = exec(IL @ gen(s2) @ IL2, len(gen(s2)) + len(IL), SE, eval(s2, EE)) .
	-- check
	red lem1S((s1 s2), il1, il2, stk, errEnv) .
	red lem1S((s1 s2), il1, il2, stk, ev) .
close

-- INDUCTION ON LOOPS
-- tc(e, s, ev, n) implies lem1S(s, il1, il2, stk, ev)

-- BASE CASE
open VERIFY-COMP .
	-- pr(EVAL)
	pr(DEL)
	op e : -> Exp .
	op s : -> Stm .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	ops ev : -> Env .
	
	-- TERMINATION HYPOTHESIS
		-- tc(e, s, ev, 0) : no loop
		eq evalExp(e,ev) = 0 .
	
	-- LEMMAS
		-- lemma Ladd (reversed)
		eq len(IL2 @ IL1) = len(IL1) + len(IL2) .
		-- lemma 1E-0
		eq exec(genExp(E) @ IL2, 0, SE, EE) = exec(genExp(E) @ IL2, len(genExp(E)), evalExp(E, EE) | SE, EE) .
		-- lemma 1E-0 (with exec2 & hd)
		eq exec2(hd(genExp(E) @ IL2), IL1 @ genExp(E) @ IL2, len(IL1), SE, EE) = exec2(nth(0,IL2), IL1 @ genExp(E) @ IL2, len(IL1 @ genExp(E)), evalExp(E, EE) | SE, EE) .
		-- Nth-del theorem
		eq nth(N, IL) = hd(del(N, IL)) .
	
	-- CHECK
	red lem1S(while e {s}, il1, il2, stk, ev) .
close

-- INDUCTION CASE
open VERIFY-COMP .
	pr(EVAL)
	pr(DEL)
	ops n k : -> PNat .
	op e : -> Exp .
	op s : -> Stm .
	ops ev ev1 : -> Env .
	ops il1 il2 : -> IList .
	op stk : -> Stack&Err .
	
	-- TERMINATION HYPOTHESIS
		-- tc(e, s, ev, s(n)) : while e {s} loops s(n) times in ev
		var K : PNat .
		ceq evalExp(e,eval(K, s, ev)) = s(0) if (K < s(n)) .
		eq evalExp(e,eval(s(n), s, ev)) = 0 .
	
	-- INDUCTION HYPOTHESIS
		-- For all E, S, EV : tc(E, S, EV, n) implies eval(while E {S}, EV) = eval(n, S, EV)
		-- It is true a fortiori with E = e, S = s, EV = eval(s,ev)
		
		-- check predicate tc(e, s, eval(s,ev), n)
		eq (k < n) = true .
		red evalExp(e,eval(k, s, eval(s,ev))) = s(0) .
		red evalExp(e,eval(n, s, eval(s,ev))) = 0 .
		
		-- equation implied by predicate
		-- eq lem1S(while e {s}, il1, il2, stk, eval(s,ev)) = true .
		eq exec(il1 @ gen(while e {s}) @ il2, len(il1), stk, eval(s,ev)) = exec(il1 @ gen(while e {s}) @ il2, len(il1 @ gen(while e {s})), stk, eval(while e {s}, eval(s,ev))) .
		
	-- LEMMAS
		-- lemma Ladd (reversed)
		eq len(IL2 @ IL1) = len(IL1) + len(IL2) .
		-- lemma 1E-0
		eq exec(genExp(E) @ IL2, 0, SE, EE) = exec(genExp(E) @ IL2, len(genExp(E)), evalExp(E, EE) | SE, EE) .
		-- lemma 1E-0 (with exec2 & hd)
		eq exec2(hd(genExp(E) @ IL2), IL1 @ genExp(E) @ IL2, len(IL1), SE, EE) = exec2(nth(0,IL2), IL1 @ genExp(E) @ IL2, len(IL1 @ genExp(E)), evalExp(E, EE) | SE, EE) .
		-- lemma 1S (version with exec2)
		eq exec2(nth(0,gen(S) @ IL2), IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec2(nth(0,IL2), IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .
		-- lemma 1S (version with exec2 and hd)
		eq exec2(hd(gen(S) @ IL2), IL1 @ gen(S) @ IL2, len(IL1), SE, EE) = exec2(nth(0,IL2), IL1 @ gen(S) @ IL2, len(IL1 @ gen(S)), SE, eval(S, EE)) .
		-- Nth-del theorem
		eq nth(N, IL) = hd(del(N, IL)) .
		-- lemma 2
		eq exec2(bjump(len(IL1) + N),IL,len(IL1) + PC,SE,EE) = exec2(bjump(N),IL,PC,SE,EE) .
		-- lemma 2-0
		eq exec2(bjump(len(IL1)),IL,PC + len(IL1),SE,EE) = exec2(bjump(0),IL,PC,SE,EE) .
			-- When PC = 0
			eq exec2(bjump(len(IL1)),IL,len(IL1),SE,EE) = exec2(bjump(0),IL,0,SE,EE) .
		
	-- Rewriting evalExp(e,ev)
		red evalExp(e,ev) = evalExp(e,eval(0, s, ev)) .
		red 0 < s(n) .
		-- provided two previous scores are true:
		eq evalExp(e,ev) = s(0) .
		
	-- red lem1S(while e {s}, il1, il2, stk, ev) .

	-- CHECK (part 1)
	-- 3-stepped verification to verify that:
		-- exec(il1 @ gen(while e {s}) @ il2, len(il1), stk, ev) = exec(il1 @ gen(while e {s}) @ il2,len(il1),stk,eval(s,ev))
			op pca : -> PNat .
			ops ila il1b il2b : -> IList .
			
			eq pca = s(s((len(il1) + len(genExp(e))))) .
			eq ila = (il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s))))) | (gen(s) @ (bjump(s(s((len(gen(s)) + len(genExp(e)))))) | il2)))))) .
			
			eq il1b = il1 @ (genExp(e) @ (jumpOnCond(s(s(0))) | (jump(s(s(len(gen(s))))) | iln))) .
			
			eq il2b = (bjump(s(s((len(gen(s)) + len(genExp(e)))))) | il2) .
			
		-- step 1 : LHS = f(X)
			-- red exec(il1 @ gen(while e {s}) @ il2, len(il1), stk, ev) = exec(il1 @ gen(while e {s}) @ il2, pca, stk, ev) .
			red exec(il1 @ gen(while e {s}) @ il2, len(il1), stk, ev) = exec2(hd(gen(s) @ il2b),il1b @ gen(s) @ il2b, pca, stk, ev) .
		-- step 2 : X = Y
			red ila = il1b @ gen(s) @ il2b .
			red len(il1b) = pca .
			
		-- step 3 : f(Y) = RHS'
			eq eval(s,ev) = ev1 . -- eval(s,ev) is a normal environment
			-- This is implicitly verified by the fact that evalExp(e,eval(s,ev)) = s(0) not in ErrPNat
			-- set trace whole on
			red exec2(hd(gen(s) @ il2b),il1b @ gen(s) @ il2b, len(il1b), stk, ev) = exec(il1 @ gen(while e {s}) @ il2,len(il1),stk,eval(s,ev)) .
			-- set trace whole off
			
	-- CHECK (part 2)
	-- Using IH to prove that:
		-- exec(il1 @ gen(while e {s}) @ il2,len(il1),stk,eval(s,ev)) = exec(il1 @ gen(while e {s}) @ il2,len(il1 @ gen(while e {s})),stk,eval(while e {s},ev))
		
		-- step 1
			-- Induction hypothesis ensures that:
			-- exec(il1 @ gen(while e {s}) @ il2, len(il1), stk, eval(s,ev)) = exec(il1 @ gen(while e {s}) @ il2, len(il1 @ gen(while e {s})), stk, eval(while e {s}, eval(s,ev)))
			
		-- step 2
			-- In induction hypothesis, there is at least one loop, thus ensuring that eval(while e {s}, eval(s,ev)) = eval(while e {s}, ev)
			red exec(il1 @ gen(while e {s}) @ il2, len(il1 @ gen(while e {s})), stk, eval(while e {s}, eval(s,ev))) = exec(il1 @ gen(while e {s}) @ il2, len(il1 @ gen(while e {s})), stk, eval(while e {s}, ev)) .
close
